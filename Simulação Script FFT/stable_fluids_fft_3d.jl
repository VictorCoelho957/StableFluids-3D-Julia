"""
Resolve as equaÃ§Ãµes do fluxo de fluidos usando o mÃ©todo "Stable Fluids" de Jos Stam com a FFT para obter simulaÃ§Ãµes ultra-rÃ¡pidas. Estende a versÃ£o 2D deste cÃ³digo para 3D.

Momento:           âˆ‚u/âˆ‚t + (u â‹… âˆ‡) u = âˆ’ 1/Ï âˆ‡p + Î½ âˆ‡Â²u + f

Incompressibilidade:  âˆ‡ â‹… u = 0

u:  Velocidade (vetor 3D)  
p:  PressÃ£o  
f:  ForÃ§a  
Î½:  Viscosidade CinÃ©tica  
Ï:  Densidade  
t:  Tempo  
âˆ‡:  Operador Nabla (definindo convecÃ§Ã£o nÃ£o-linear, gradiente e divergÃªncia)  
âˆ‡Â²: Operador de Laplace  

----

Um domÃ­nio em forma de cubo unitÃ¡rio com CondiÃ§Ãµes de Contorno PeriÃ³dicas (por exemplo, o que sai pelo topo entra novamente pela base).

           +--------+  
          /        /|  
         /        / |  
        +--------+  |  
        |        |  |  
        |        |  +  
        |        | /  
        |        |/  
        +--------+  

-> Dois patches de forÃ§a em direÃ§Ãµes opostas, centralizados verticalmente, mas ligeiramente deslocados.

-----

EstratÃ©gia de SoluÃ§Ã£o:

-> ComeÃ§ar com velocidade zero em todos os lugares: u = [0, 0, 0]

1. Adicionar forÃ§as

    wâ‚ = u + Î”t f

2. Convectar por auto-advecÃ§Ã£o (definir o valor na localizaÃ§Ã£o atual como sendo o valor na posiÃ§Ã£o rastreada no fluxo de corrente.) -> incondicionalmente estÃ¡vel

    wâ‚‚ = wâ‚(p(x, âˆ’Î”t))

3. Difundir e Projetar no DomÃ­nio de Fourier

    3.1 TransformaÃ§Ã£o para o DomÃ­nio de Fourier  

        wâ‚‚ â†’ wâ‚ƒ
    
    3.2 Difundir por "filtragem de baixa frequÃªncia" (convoluÃ§Ã£o Ã© multiplicaÃ§Ã£o no DomÃ­nio de Fourier)  

        wâ‚„ = exp(âˆ’ kÂ² Î½ Î”t) wâ‚ƒ
    
    3.3 Calcular a (pseudo-)pressÃ£o no DomÃ­nio de Fourier avaliando a divergÃªncia no DomÃ­nio de Fourier  

        q = wâ‚„ â‹… k / ||k||â‚‚
    
    3.4 Corrigir as velocidades para que sejam incompressÃ­veis  

        wâ‚… = wâ‚„ âˆ’ q k / ||k||â‚‚
    
    3.5 TransformaÃ§Ã£o Inversa de volta ao domÃ­nio espacial  

        wâ‚† â† wâ‚…

4. Repetir

k = [k_x, k_y, k_z] sÃ£o as frequÃªncias espaciais (= nÃºmeros de onda).

A Transformada de Fourier prescreve implicitamente as CondiÃ§Ãµes de Contorno PeriÃ³dicas.

-------

MudanÃ§as em relaÃ§Ã£o ao vÃ­deo original (https://youtu.be/bvPi6XwdM0U)

1. Alterar o prÃ©-fator de tempo na forÃ§a para aplicÃ¡-lo por mais tempo (veja o passo (1) no loop temporal).

2. Usar "clamping" periÃ³dico e aumentar o nÃºmero de passos de tempo.

5. Grupo - MudanÃ§a e ConfiguraÃ§Ã£o em relaÃ§Ã£o ao video original - Andre Moura e Victor Coelho.
"""

# Importa pacotes necessÃ¡rios:
# - FFTW: biblioteca para realizar transformadas rÃ¡pidas de Fourier (Fast Fourier Transform - FFT)
# - WriteVTK: permite salvar dados em arquivos no formato VTK para visualizaÃ§Ã£o
# - ProgressMeter: exibe uma barra de progresso para loops
# - Interpolations: fornece mÃ©todos para interpolaÃ§Ã£o em vÃ¡rias dimensÃµes
# - LinearAlgebra: inclui operaÃ§Ãµes de Ã¡lgebra linear como norm e transformaÃ§Ãµes matriciais

using FFTW
using WriteVTK
using ProgressMeter
using Interpolations
using LinearAlgebra

N_POINTS = 44  # Aumentar a resoluÃ§Ã£o da malha de 40 para 44 pontos (Mesh). (Autor Original)
KINEMATIC_VISCOSITY = 0.001 #  0.0001/ Aumenta a viscosidade para tornar o fluido mais resistente ao movimento.
TIME_STEP_LENGTH = 0.02 # 0.01/  Dobra o passo de tempo.
N_TIME_STEPS = 160 # 100/ Aumenta o nÃºmero de passos de tempo para prolongar a simulaÃ§Ã£o.

function backtrace!(
    backtraced_positions,
    original_positions,
    direction,
)
    # Passo de Euler para trÃ¡s no tempo e, periodicamente, limita no intervalo [0.0, 1.0]
    backtraced_positions[:] = mod1.(
        original_positions - TIME_STEP_LENGTH * direction,
        1.0
    )
end

function interpolate_positions!(
    field_interpolated,
    field,
    interval_x,
    interval_y,
    interval_z,
    query_points_x,
    query_points_y,
    query_points_z,
)
    interpolator = LinearInterpolation(
        (interval_x, interval_y, interval_z),
        field,
    )
    field_interpolated[:] = interpolator.(query_points_x, query_points_y, query_points_z)
end

function main()
    element_length = 1.0 / (N_POINTS - 1)  # Quanto maior o N_POINTS, menor o elemento MESH.
    x_interval = 0.0:element_length:1.0
    y_interval = 0.0:element_length:1.0
    z_interval = 0.0:element_length:1.0
    
    # Semelhante ao meshgrid no NumPy  /
    #Essas variÃ¡veis representam as coordenadas x, ð‘¦ e z de todos os pontos na malha 3D.
    coordinates_x = [x for x in x_interval, y in y_interval, z in z_interval]
    coordinates_y = [y for x in x_interval, y in y_interval, z in z_interval]
    coordinates_z = [z for x in x_interval, y in y_interval, z in z_interval]

    wavenumbers_1d = fftfreq(N_POINTS) .* N_POINTS

    wavenumbers_x = [k_x for k_x in wavenumbers_1d, k_y in wavenumbers_1d, k_z in wavenumbers_1d]
    wavenumbers_y = [k_y for k_x in wavenumbers_1d, k_y in wavenumbers_1d, k_z in wavenumbers_1d]
    wavenumbers_z = [k_z for k_x in wavenumbers_1d, k_y in wavenumbers_1d, k_z in wavenumbers_1d]
    wavenumbers_norm = [norm([k_x, k_y, k_z]) for k_x in wavenumbers_1d, k_y in wavenumbers_1d, k_z in wavenumbers_1d]

    decay = exp.(- TIME_STEP_LENGTH .* KINEMATIC_VISCOSITY .* wavenumbers_norm.^2)

    wavenumbers_norm[iszero.(wavenumbers_norm)] .= 1.0
    normalized_wavenumbers_x = wavenumbers_x ./ wavenumbers_norm
    normalized_wavenumbers_y = wavenumbers_y ./ wavenumbers_norm
    normalized_wavenumbers_z = wavenumbers_z ./ wavenumbers_norm

    # Defina as forÃ§as
force_x = 200.0 .* ( #100
        ifelse.(
            (coordinates_x .> 0.2) # 0.2
            .&
            (coordinates_x .< 0.4) # 0.3
            .&
            (coordinates_y .> 0.46) # 0.45
            .&
            (coordinates_y .< 0.53) # 0.52
            .&
            (coordinates_z .> 0.46) # 0.45
            .&
            (coordinates_z .< 0.53), # 0.52
            1.0,
            0.0,
        )
        -
        ifelse.(
            (coordinates_x .> 0.7) # 0.7
            .&
            (coordinates_x .< 0.9) # 0.8
            .&
            (coordinates_y .> 0.49) #0.48
            .&
            (coordinates_y .< 0.56) #0.55
            .&
            (coordinates_z .> 0.49) #0.48
            .&
            (coordinates_z .< 0.56), #0.55
            1.0,
            0.0,

        )
    )

    # PrÃ©-alocar todos os arrays
    backtraced_coordinates_x = zero(coordinates_x)
    backtraced_coordinates_y = zero(coordinates_y)
    backtraced_coordinates_z = zero(coordinates_z)

    velocity_x = zero(coordinates_x)
    velocity_y = zero(coordinates_y)
    velocity_z = zero(coordinates_z)

    velocity_x_prev = zero(velocity_x)
    velocity_y_prev = zero(velocity_y)
    velocity_z_prev = zero(velocity_z)

    velocity_x_fft = zero(velocity_x)
    velocity_y_fft = zero(velocity_y)
    velocity_z_fft = zero(velocity_z)
    pressure_fft = zero(coordinates_x)

    velocity_x_trajectory = []
    velocity_y_trajectory = []
    velocity_z_trajectory = []

    @showprogress "Timestepping ..." for iter in 1:N_TIME_STEPS

        # (1) Aplicar as forÃ§as
        time_current = (iter - 1) * TIME_STEP_LENGTH
        pre_factor = max(1 - time_current, 0.0)
        velocity_x_prev += TIME_STEP_LENGTH * pre_factor * force_x

        # (2) Auto-advecÃ§Ã£o por retrotraÃ§Ã£o e interpolaÃ§Ã£o
        backtrace!(backtraced_coordinates_x, coordinates_x, velocity_x_prev)
        backtrace!(backtraced_coordinates_y, coordinates_y, velocity_y_prev)
        backtrace!(backtraced_coordinates_z, coordinates_z, velocity_z_prev)
        interpolate_positions!(
            velocity_x,
            velocity_x_prev,
            x_interval,
            y_interval,
            z_interval,
            backtraced_coordinates_x,
            backtraced_coordinates_y,
            backtraced_coordinates_z,
        )
        interpolate_positions!(
            velocity_y,
            velocity_y_prev,
            x_interval,
            y_interval,
            z_interval,
            backtraced_coordinates_x,
            backtraced_coordinates_y,
            backtraced_coordinates_z,
        )
        interpolate_positions!(
            velocity_z,
            velocity_z_prev,
            x_interval,
            y_interval,
            z_interval,
            backtraced_coordinates_x,
            backtraced_coordinates_y,
            backtraced_coordinates_z,
        )

        # (3.1) Transformar para o DomÃ­nio de Fourier
        velocity_x_fft = fft(velocity_x)
        velocity_y_fft = fft(velocity_y)
        velocity_z_fft = fft(velocity_z)

        # (3.2) Difundir por filtragem passa-baixa
        velocity_x_fft .*= decay
        velocity_y_fft .*= decay
        velocity_z_fft .*= decay

        # (3.3) Calcular a Pseudo-PressÃ£o pela DivergÃªncia no DomÃ­nio de Fourier
        pressure_fft = (
            velocity_x_fft .* normalized_wavenumbers_x
            +
            velocity_y_fft .* normalized_wavenumbers_y
            +
            velocity_z_fft .* normalized_wavenumbers_z
        )

        # (3.4) Projetar as velocidades para serem incompressÃ­veis
        velocity_x_fft -= pressure_fft .* normalized_wavenumbers_x
        velocity_y_fft -= pressure_fft .* normalized_wavenumbers_y
        velocity_z_fft -= pressure_fft .* normalized_wavenumbers_z

        # (3.5) Transformar de volta para o domÃ­nio espacial
        velocity_x = real(ifft(velocity_x_fft))
        velocity_y = real(ifft(velocity_y_fft))
        velocity_z = real(ifft(velocity_z_fft))

        # AvanÃ§ar no tempo
        velocity_x_prev = velocity_x
        velocity_y_prev = velocity_y
        velocity_z_prev = velocity_z

        # Salvar para visualizaÃ§Ã£o
        push!(velocity_x_trajectory, velocity_x)
        push!(velocity_y_trajectory, velocity_y)
        push!(velocity_z_trajectory, velocity_z)
    end

    paraview_collection("transient_vector") do pvd
        @showprogress "Writing out to vtk ..." for iter in 1:N_TIME_STEPS
            vtk_grid("timestep_$iter", x_interval, y_interval, z_interval) do vtk
                vtk["velocity"] = (
                    velocity_x_trajectory[iter],
                    velocity_y_trajectory[iter],
                    velocity_z_trajectory[iter],
                )
                time = (iter - 1) * TIME_STEP_LENGTH
                pvd[time] = vtk
            end
        end
    end

end

main()